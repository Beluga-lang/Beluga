%%% Classical Sequent Calculus with Cut.
%%% Author: Frank Pfenning

@ : type.        % Token (for contradiction)
%name @ D*.

cut^   : (pos A -> @)
	  -> (neg A -> @)
	  -> @.

axiom^ : (neg A -> pos A -> @).

andr^  : (pos A -> @)
	  -> (pos B -> @)
	  -> (pos (& A B) -> @).

andl1^ : (neg A -> @)
	  -> (neg (& A B) -> @).

andl2^ : (neg B -> @)
	  -> (neg (& A B) -> @).

impr^  : (neg A -> pos B -> @)
	  -> (pos (imp A B) -> @).

impl^  : (pos A -> @)
	  -> (neg B -> @)
	  -> (neg (imp A B) -> @).

orr1^  : (pos A -> @)
	 -> (pos (or A B) -> @).

orr2^  : (pos B -> @)
	  -> (pos (or A B) -> @).

orl^   : (neg A -> @)
	 -> (neg B -> @)
	 -> (neg (or A B) -> @).

notr^  : (neg A -> @)
	  -> (pos (not A) -> @).

notl^  : (pos A -> @)
	  -> (neg (not A) -> @).

truer^ : (pos (true) -> @).

falsel^ : (neg (false) -> @).

forallr^ : ({a:i} pos (A a) -> @)
	    -> (pos (forall A) -> @). 

foralll^ : {T:i} (neg (A T) -> @)
	    -> (neg (forall A) -> @).

existsr^ : {T:i} (pos (A T) -> @)
	    -> (pos (exists A) -> @).

existsl^ : ({a:i} neg (A a) -> @)
	    -> (neg (exists A) -> @).
