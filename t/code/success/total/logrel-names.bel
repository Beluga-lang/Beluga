% Completeness/soundness of algorithmic equality for declarative equality for name term language.

nm : type.
leaf : nm.
nmbin : nm -> nm -> nm.

tp : type.         --name tp T.
tp/nm :  tp.
tp/unit : tp.
prod : tp -> tp -> tp.
arr: tp -> tp -> tp.

tm : type.          --name tm E.
unit : tm.
pair : tm -> tm -> tm.
fst : tm -> tm. % ???
snd : tm -> tm. % ???
name : nm -> tm.
bin : tm -> tm -> tm.
app : tm -> tm -> tm.
lam : (tm -> tm) -> tm.

step : tm -> tm -> type.  --name step S.
step/beta : step (app (lam M) N) (M N).
step/app : step M M' -> step (app M N) (app M' N). % stepapp
step/betaprod1 : step (fst (pair M1 M2)) M1.
step/betaprod2 : step (snd (pair M1 M2)) M2.
step/proj1 : step M M' -> step (fst M) (fst M').
step/proj2 : step M M' -> step (snd M) (snd M').
step/bin : step M1' M1 -> step M2' M2 -> step (bin M1' M2') (bin M1 M2).
step/bin1 : step M1' M1 -> step (bin M1' M2) (bin M1 M2).
step/bin2 : step M2' M2 -> step (bin M1 M2') (bin M1 M2).

oft : tm -> tp -> type.
oft/app : oft M (arr T S) -> oft N T -> oft (app M N) S.
oft/lam : ({x:tm} oft x T -> oft (M x) S) -> oft (lam (\x. M x)) (arr T S).
oft/pair : oft (fst M) T1 -> oft (snd M) T2 -> oft M (prod T1 T2).
% oft/pair : oft M1 T1 -> oft M2 T2 -> oft (pair M1 M2) (prod T1 T2).
oft/bin : oft M1 tp/nm -> oft M2 tp/nm -> oft (bin M1 M2) tp/nm. % need???
% TODO: M-const is missing from the original. Why???
oft/nm : oft (name n) tp/nm.

mstep : tm -> tm -> type.  --name mstep S.
refl : mstep M M.
trans1 : step M M' -> mstep M' M'' -> mstep M M''.

% algorithmic (and path) equivalence and apartness
algeqn : tm -> tm -> tp -> type.
algeqr : tm -> tm -> tp -> type.
algapn : tm -> tm -> tp -> type.
algapr : tm -> tm -> tp -> type.

algequnit : algeqn M N tp/unit.
algeqname : mstep M P -> mstep N Q -> algeqr P Q tp/nm -> algeqn M N tp/nm.
algeqarr : ({x:tm}{xx:algeqr x x T} algeqn (app M x) (app N x) S) -> algeqn M N (arr T S).
algeqprod : algeqn (fst M) (fst N) T1 -> algeqn (snd M) (snd N) T2 -> algeqn M N (prod T1 T2).
% TODO: do we need algeqbin?
% algeqbin : algeqn M1 N1 tp/nm -> algeqn M2 N2 tp/nm -> algeqn (bin M1 M2) (bin N1 N2) tp/nm.

algeqapp : algeqr M1 M2 (arr T S) -> algeqn N1 N2 T -> algeqr (app M1 N1) (app M2 N2) S.
algeqproj1 : algeqr P Q (prod T1 T2) -> algeqr (fst P) (fst Q) T1.
algeqproj2 : algeqr P Q (prod T1 T2) -> algeqr (snd P) (snd Q) T2.
algeqbinp : algeqr P1 Q1 tp/nm -> algeqr P2 Q2 tp/nm -> algeqr (bin P1 P2) (bin Q1 Q2) tp/nm.
% TODO: why is QAP-Name(Base) missing in the original???
algeqnamep : algeqr (name n) (name n) tp/nm.
% algeqr-trans : algeqr P Q T -> algeqr Q R T -> algeqr P R T.

algapunit : algapn M N tp/unit.
algapname : mstep M P -> mstep N Q -> algapr P Q tp/nm -> algapn M N tp/nm.
algaparr : ({x:tm}{y:tm}{xy:algeqr x y T}{yx:algeqr y x T} algapn (app M x) (app N y) S) -> algapn M N (arr T S).
algapbin1 : algapn M1 N1 tp/nm -> algapn (bin M1 M2) (bin N1 N2) tp/nm.
algapbin2 : algapn M2 N2 tp/nm -> algapn (bin M1 M2) (bin N1 N2) tp/nm.
algappair : algapn (fst M) (fst N) T1 -> algapn (snd M) (snd N) T2 -> algapn M N (prod T1 T2).

algapapp : algapr P Q (arr T S) -> algeqn M N T -> algapr (app P M) (app Q N) T2.
algapproj1 : algapr P Q (prod T1 T2) -> algapr (fst P) (fst Q) T1.
algapproj2 : algapr P Q (prod T1 T2) -> algapr (snd P) (snd Q) T2.
algapbin : algapr P1 Q1 tp/nm -> algapr P2 Q2 tp/nm -> algapr (bin P1 P2) (bin Q1 Q2) tp/nm.
% TODO:
% algapbin vs algapbin1/2??? Are any of these needed???
% algapvar missing in original???
% algapname missing??? how to encode inequality of names???

% schema tctx = some [t:tp] block x:tm, y:oft x t, z:algeqr x x t;
schema rctx = some [t:tp] block x:tm, xt:oft x t, xx:algeqr x x t
                        + block x:tm, y:tm, xt:oft x t, yt:oft y t, xy:algapr x y t, yx:algapr y x t;


% Logical equivalence. _|-_~~_:_ in the paper

tmpair : type.
sim : tm -> tm -> tmpair.
--infix sim 5 right.

stratified Log : (g:rctx) [g |- tmpair] -> [ |- tp] -> ctype =
| LogUnit : Log [g |- M sim N ] [|- tp/unit]
| LogName : [g |- algeqn M N tp/nm]
            -> Log [g |- M sim N ] [|- tp/nm]
| LogProd : Log [g |- (fst M) sim (fst N) ] [|- T1]
            -> Log [g |- (snd M) sim (snd N) ] [|- T2]
            -> Log [g |- M sim N ] [|- prod T1 T2]
| LogArr  : {M1:[g |- tm]}{M2:[g |- tm]}
             ({h:rctx} {$R:[h |- g]} {N1:[h |- tm]}{D1:[h |- oft N1 T[]]}{N2:[h |- tm]}{D2:[h |- oft N2 T[]]}
                Log [h |- N1 sim N2] [ |- T]
              -> Log [h |- (app M1[$R] N1) sim (app M2[$R] N2)] [|- S])
            -> Log [g |- M1 sim M2] [ |- arr T S]
;

% Monotonicity of logical equivalence
rec logEq_Monotone : {h:rctx}{$W:[h |- g]} Log [g |- M1 sim M2] [ |- A]
                   -> Log [h |- M1[$W] sim M2[$W]] [ |- A] =
/ total e (logEq_Monotone g h m1 m2 a iv e) /
mlam h,$W => fn e => case e of
| LogName [g |- P] => LogName [h |- P[$W]]
| LogArr [g |-  M1] [g |-  M2] f =>
  LogArr [h |-  M1[$W]] [h |-  M2[$W]]
   (mlam h0,$W2,N1,D1,N2,D2 => fn rn =>
    f [h0] [h0 |- $W[$W2]] [h0 |-  N1] [_ |- D1] [h0 |-  N2] [_ |- D2] rn
   )
| LogUnit => LogUnit
| LogProd leq1 leq2 =>
  let ih1 = logEq_Monotone [ h ] [ h |- $W ] leq1 in
  let ih2 = logEq_Monotone [ h ] [ h |- $W ] leq2 in
  LogProd ih1 ih2
;


% Main lemma to escape logical relation
rec reflect : {A:[ |- tp]} [g |- algeqr M1 M2 A[]] -> Log [g |- M1 sim M2] [ |- A] =
/ total a (reflect g m1 m2 a) /
mlam A => fn r => case [ |- A] of
| [ |- tp/nm] =>
    let [g |- R ] = r in
    LogName [g |- algeqname refl refl R]
| [ |- arr A B] =>
  let [g |- R] = r in
  LogArr [g |- _] [g |-  _]
   (mlam h,$W,N1,D1,N2,D2 => fn rn =>
    let [h |- S] = reify [|- A] rn in
    reflect [ |- B] [h |- algeqapp R[$W] S]
   )
| [ |- prod T1 T2] =>
  let [g |- R] = r in
  let p1 = [g |- algeqproj1 R] in
  let p2 = [g |- algeqproj2 R] in
  let ih1 = reflect [|- T1] p1 in
  let ih2 = reflect [|- T2] p2 in
  LogProd ih1 ih2
| [ |- tp/unit] => LogUnit
and reify : {A:[|- tp]} Log [g |- M1 sim M2] [ |- A] -> [g |- algeqn M1 M2 A[]] =
 / total a (reify g m1 m2 a) /
mlam A => fn e => case [ |- A] of
| [ |- tp/nm ] =>
  let LogName a = e in a
| [ |- arr A B] =>
  let LogArr [g |-  M1] [g |-  M2] f = e in
  let [g,b:block x:tm,xt:oft x A[],xx:algeqr x x A[] |- R[..,b.1,b.3]]
      = reify [ |- B ] (f [g,b:block x:tm,xt:oft x A[],xx:algeqr x x A[]]
                          [g,b |- ..] [g,b |- b.1] [g,b |- b.2] [g,b |- b.1] [g,b |- b.2]
        (reflect [ |- A ] [g,b:block x:tm,xt:oft x A[],xx:algeqr x x A[] |- b.3]))
  in [g |- algeqarr (\x. \y. R)]
| [ |- prod T1 T2] =>
  let LogProd leq1 leq2 = e in
  let [g |- AEq1] = reify [|- T1] leq1 in
  let [g |- AEq2] = reify [|- T2] leq2 in
  [g |- algeqprod AEq1 AEq2]
| [ |- tp/unit] => [_ |- algequnit] % TODO: how to access g for _? not sure what _ does.
;


% Transitivity of mstep
rec mstep_trans : (g:rctx) [g |-  mstep M N] -> [g |-  mstep N P] -> [g |-  mstep M P] =
/ total s1 (mstep_trans g m n p s1 s2) /
fn s1 => fn s2 => case s1 of
| [g |-  refl] => let [g |-  S2] = s2 in [g |-  S2]
| [g |-  trans1 S1 (MS1)] => let [g |-  MSR] = mstep_trans [g |-  MS1] s2 in
  [g |-  trans1 S1 (MSR)]
;


% Boxed form of trans1 for convenience
rec trans1' : (g:rctx) [g |-  step M N] -> [g |-  mstep N P] -> [g |-  mstep M P] =
/ total (trans1') /
fn s1 => fn s2 => case (s1,s2) of
| ([g |-  S], [g |-  MS]) => [g |-  trans1 S (MS)]
; % TODO: Are these actually necessary?


% multi-step reduction under the function position of application
rec mstep_app : (g:rctx) [g |-  mstep M N] -> [g |-  mstep (app M P) (app N P)] =
/ total s1 (mstep_app g m n p s1) /
fn s1 => case s1 of
| [g |-  refl] => [g |-  refl]
| [g |-  trans1 S (MS)] => trans1' [g |-  step/app S] (mstep_app [g |-  MS])
;

rec mstep_proj1 : (g:rctx) [g |- mstep M N] -> [g |- mstep (fst M) (fst N)] =
/ total s (mstep_proj1 g m n s) /
fn s => case s of
| [g |- refl] => [g |- refl]
| [g |- trans1 S MS] => trans1' [g |-  step/proj1 S] (mstep_proj1 [g |- MS])
;

rec mstep_proj2 : (g:rctx) [g |- mstep M N] -> [g |- mstep (snd M) (snd N)] =
/ total s (mstep_proj2 g m n s) /
fn s => case s of
| [g |- refl] => [g |- refl]
| [g |- trans1 S MS] => trans1' [g |-  step/proj2 S] (mstep_proj2 [g |- MS])
;

rec mstep_bin : (g:rctx) [g |- mstep M1 N1] -> [g |- mstep M2 N2] -> [g |- mstep (bin M1 M2) (bin N1 N2)] =
/ total s1 (mstep_bin g m1 n1 m2 n2 s1 s2) /
% / total s2 (mstep_bin g m1 n1 m2 n2 s1 s2) /
fn s1 => fn s2 => case s1 of
| ([g |- refl] : [g |- mstep M1 M1]) => (case s2 of
  | ([g |- refl]) => [g |- refl]
  | [g |- trans1 S MS] =>
    let ([g |- MS] : [g |- mstep N N2]) = [g |- MS] in
    % let (thing : [g |- mstep (bin M1 N) (bin M1 N2)]) = mstep_bin ? [g |- MS] in
    ?
  )
| [g |- trans1 S MS] => (case s2 of
  | [g |- refl] => ?
  | [g |- trans1 S MS] => ?)
;


% rec mstep_binproj1 : (g:rctx) [g |- mstep (bin M1 M2) (bin N1 N2)] -> [g |- mstep M1 N1] =
% / total s (mstep_binproj1 g m1 m2 n1 n2 s) /
% fn s => case s of
% | [g |- refl] => [g |- refl]
% | [g |- trans1 (step/bin S1 S2) MS] => [g |- S1]
% ;

% Logical equality is backward closed under weak head reduction
% Induction is on T. Make T explicit
rec closed : {T:[|- tp]}
     [g |-  mstep N1 M1] -> [g |-  mstep N2 M2]
  -> Log [g |- M1 sim M2] [ |- T]
  -> Log [g |- N1 sim N2] [ |- T] =
 / total t (closed g n1 m1 n2 m2 t) /
mlam T => fn s1,s2,e => case [|- T] of
| [|- arr T1 T2] =>
  let LogArr [g |-  M1] [g |-  M2] f = e in
  let [g |-  S1] = s1 in
  let [g |-  S2] = s2 in
  LogArr [g |-  _] [g |-  _] (mlam h,$W,P1,D1,P2,D2 => fn rp =>
   let q0 = f [h] [h |- $W] [h |-  P1] [h |- D1] [h |-  P2] [h |- D2] rp in
   closed [|- T2] (mstep_app [h |-  S1[$W]] ) (mstep_app [h |-  S2[$W]]) q0
  )
| [ |- prod T1 T2] =>
  let LogProd leq1 leq2 = e in
  let ih1 = closed [|- T1] (mstep_proj1 s1) (mstep_proj1 s2) leq1 in
  let ih2 = closed [|- T2] (mstep_proj2 s1) (mstep_proj2 s2) leq2 in
  LogProd ih1 ih2
| [ |- tp/unit] => LogUnit
% TODO: there is a bug where the file doesn't load with this case at the top.
% TODO: this tp/nm case is more complicated (see commented block below)
%       if we have algeqbin at the "name term level"
%       (not just the path level) - do we need such a rule?
| [|- tp/nm] =>
  let LogName [g |- algeqname (S1') (S2') A] = e in
  let [g |- S1''] = mstep_trans s1 [g |- S1'] in
  let [g |- S2''] = mstep_trans s2 [g |- S2'] in
  LogName [g |- algeqname (S1'') (S2'') A]
;
% | [|- tp/nm] => case e of
%   | LogName [g |- algeqname S1' S2' A] =>
%     % let LogName [g |- algeqname (S1') (S2') A] = e in
%     let [g |- S1''] = mstep_trans s1 [g |- S1'] in
%     let [g |- S2''] = mstep_trans s2 [g |- S2'] in
%     LogName [g |- algeqname (S1'') (S2'') A]
%   | LogName [g |- algeqbin A1 A2] =>
%     let [g |- S1] = s1 in
%     ?
% ;


% Symmetry of algorithmic equivalence
rec algEqRSym : (g:rctx)
  [g |- algeqr N1 N2 T]
-> [g |- algeqr N2 N1 T] =
/ total a (algEqRSym g n1 n2 t a) /
fn a => case a of
| [g |- #p.3] => [g |- #p.3]
| [g |- algeqapp R N] =>
  let [g |- R'] = algEqRSym [g |- R] in
  let [g |- N'] = algEqNSym [g |- N] in
  [g |- algeqapp (R') (N')]
| [g |- algeqproj1 A] => let [g |- A'] = algEqRSym [g |- A] in [g |- algeqproj1 A']
| [g |- algeqproj2 A] => let [g |- A'] = algEqRSym [g |- A] in [g |- algeqproj2 A']
| [g |- algeqbinp A1 A2] =>
  let [g |- A1'] = algEqRSym [g |- A1] in
  let [g |- A2'] = algEqRSym [g |- A2] in
  [g |- algeqbinp A1' A2']
| [g |- algeqnamep] => [g |- algeqnamep]
and algEqNSym : (g:rctx)
  [g |- algeqn N1 N2 T]
-> [g |- algeqn N2 N1 T] =
/ total a (algEqNSym g n1 n2 t a) /
fn a => case a of
| [g |- algeqname S1 S2 R] =>
  let [g |- R'] = algEqRSym [g |- R] in
  [g |- algeqname S2 S1 (R')]
| [g |- algeqarr \x.\y.N] =>
  let (a : [g |- algeqn _ _ (arr T[] S[])]) = a in
  % key was to swap b.1 with b.2, as well as b.6 with b.5
  let [g,b:block x:tm,xt:oft x T[],xx:algeqr x x T[] |- N'[..,b.1,b.3]] =
      algEqNSym [g,b:block x:tm,xt:oft x T[],z:algeqr x x T[] |- N[..,b.1,b.3]]
  in [g |- algeqarr (\x.\y.N')]
| [g |- algequnit ] =>  [g |- algequnit]
| [g |- algeqprod A1 A2] =>
  let [g |- A1'] = algEqNSym [g |- A1] in
  let [g |- A2'] = algEqNSym [g |- A2] in
  [g |- algeqprod A1' A2']
;


% % Symmetry of logical equivalence
rec logEqSym :
   {T:[|- tp]}Log [g |- M1 sim M2] [ |- T]
 -> Log [g |- M2 sim M1] [ |- T] =
/ total t (logEqSym g m1 m2 t) /
mlam T => fn e => case [|- T] of
| [|- tp/nm] =>
  let LogName a = e in  LogName (algEqNSym a)
| [|- arr T1 T2] =>
  let LogArr [g |-  M1] [g |-  M2] f = e in
  LogArr [g |-  M2] [g |-  M1] (mlam h,$W,N1,D1,N2,D2 => fn rn =>
					let e' = logEqSym [|- T1] rn in
					logEqSym [|- T2] (f [h] [h |- $W] [h |-  N2] [h |- D2] [h |-  N1] [h |- D1] e')
				                       )
| [ |- prod T1 T2] =>
  let LogProd e1 e2 = e in
  let ih1 = logEqSym [|- T1] e1 in
  let ih2 = logEqSym [|- T2] e2 in
  LogProd ih1 ih2
| [ |- tp/unit] => LogUnit
;

% % Several of the following lemmas are to establish transitivity of algorithmic equivalenc

% Equality on types
eqtp : tp -> tp -> type.
refltp : eqtp T T.

% The types from algorithmic path equivalence are uniquely determined
rec algEqRUnique : (g:rctx)
  [g |- algeqr N1 N2 T[]]
-> [g |- algeqr N2 N3 S[]]
-> [|- eqtp T S] =
/ total e1 (algEqRUnique g n1 n2 n3 t s e1) /
fn e1 => fn e2 => case e1 of
| [g |- #p.3] => let [g |- #q.3] = e2 in [|- refltp]
| [g |- algeqapp R N] =>
  let [g |- algeqapp (R') (N')] = e2 in
  let [|- refltp] = algEqRUnique [g |- R] [g |- R'] in [|- refltp]
| [g |- algeqproj1 A] =>
  let [g |- algeqproj1 A'] = e2 in
  let [|- refltp] = algEqRUnique [g |- A] [g |- A'] in [|- refltp]
| [g |- algeqproj2 A] =>
  let [g |- algeqproj2 A'] = e2 in
  let [|- refltp] = algEqRUnique [g |- A] [g |- A'] in [|- refltp]
| [g |- algeqbinp A1 A2] => let [g |- algeqbinp A1' A2'] = e2 in [|- refltp]
| [g |- algeqnamep] => let [g |- algeqnamep] = e2 in [|- refltp]
;

eqtm : tm -> tm -> type.
refltm : eqtm M M.

% Determinacy of weak head reduction
rec determinacy : (g:rctx) [g |- step M N] -> [g |-  step M P] -> [g |- eqtm N P] =
/ total s1 (determinacy g m n p s1) /
fn s1 => fn s2 => case s1 of
| [g |-  step/beta] => let [g |-  step/beta] = s2 in [g |- refltm ]
| [g |-  step/app S1] =>
  let [g |-  step/app S2] = s2 in
  let [g |- refltm] = determinacy [g |-  S1] [g |-  S2] in [g |- refltm]
| [g |- step/betaprod1] => let [g |-  step/betaprod1] = s2 in [g |- refltm]
| [g |- step/betaprod2] => let [g |-  step/betaprod2] = s2 in [g |- refltm]
| [g |- step/proj1 S1] =>
  let [g |-  step/proj1 S2] = s2 in
  let [g |- refltm] = determinacy [g |-  S1] [g |-  S2] in [g |- refltm]
| [g |- step/proj2 S1] =>
  let [g |-  step/proj2 S2] = s2 in
  let [g |- refltm] = determinacy [g |-  S1] [g |-  S2] in [g |- refltm]
| [g |- step/bin S11 S12] => ?
%  let [g |-  step/bin S21 S22] = s2 in
%  let [g |- refltm] = determinacy [g |-  S11] [g |-  S21] in
%  let [g |- refltm] = determinacy [g |-  S12] [g |-  S22] in
%  [g |- refltm]
| todo => ?
;


inductive HasCommonReduct : (g:rctx) [g |-  tm] -> [g |-  tm] -> ctype =
| Common : [g |-  mstep M1 N] -> [g |-  mstep M2 N] -> HasCommonReduct [g |-  M1] [g |-  M2];

% Confluence is trivial because it's deterministic. This is just a convenient
% way to work with determinacy
rec confluence : [g |-  mstep M N] -> [g |-  mstep M P] ->
                 HasCommonReduct [g |-  N] [g |-  P] =
/ total s1 (confluence g m n p s1) /
fn s1 => fn s2 => case s1 of
| [g |-  refl ] => Common s2 [g |-  refl]
| [g |-  trans1 S1 (MS1)] =>
  case s2 of
  | [g |-  refl] => Common [g |-  refl] s1
  | [g |-  trans1 S2 (MS2)] =>
    let [g|- refltm] = determinacy [g |-  S1] [g |-  S2] in
    confluence [g |-  MS1] [g |-  MS2]
;

LF empty : type = ;

% Terms in algorithmic path equivalence cannot step
rec neutral_doesnt_step : (g:rctx)
  [g |-  step M N]
-> [g |- algeqr M P T]
-> [ |- empty] =
/ total s (neutral_doesnt_step g m n p t s ) /
fn s => fn e => case s of
| [g |- step/beta] => let [g |- algeqapp R N] = e in impossible [g |- R]
| [g |- step/app S] => let [g |- algeqapp R N] = e in neutral_doesnt_step [g |- S] [g |- R]
| [g |- step/betaprod1] => let [g |- algeqproj1 A] = e in impossible [g |- A]
| [g |- step/betaprod2] => let [g |- algeqproj2 A] = e in impossible [g |- A]
| [g |- step/proj1 S] => let [g |- algeqproj1 A] = e in neutral_doesnt_step [g |- S] [g |- A]
| [g |- step/proj2 S] => let [g |- algeqproj2 A] = e in neutral_doesnt_step [g |- S] [g |- A]
| [g |- step/bin S1 S2] => let [g |- algeqbinp A1 A2] = e in neutral_doesnt_step [g |- S1] [g |- A1]
| todo => ?
;

% Similarly for multi-step reduction
rec neutral_mstep : (g:rctx)
  [g |-  mstep M N]
-> [g |- algeqr M P T]
-> [g |- eqtm M N] =
/ total  (neutral_mstep) /
fn s => fn e => case s of
| [g |-  refl] => [g |- refltm]
| [g |-  trans1 S1 (MS)] => impossible (neutral_doesnt_step [g |-  S1] e) in [];

% Corollary of determinacy
rec determinacy' : (g:rctx)
  [g |-  mstep M M1] -> [g |-  mstep M M2]
-> [g |- algeqr N1 M1 T]
-> [g |- algeqr M2 N2 T]
-> [g |- eqtm M1 M2] =
/ total (determinacy') /
fn s1 => fn s2 => fn e1 => fn e2 => case (confluence s1 s2) of
| Common s1' s2' =>
  let [g|-refltm] = neutral_mstep s1' (algEqRSym e1) in
  let [g|-refltm] = neutral_mstep s2' e2 in
  [g|-refltm]
;

% Transitivity of algorithmic equivalence
rec algEqRTrans : (g:rctx)
  [g |- algeqr N1 N2 T]
-> [g |- algeqr N2 N3 T]
-> [g |- algeqr N1 N3 T] =
/ total a1 (algEqRTrans g n1 n2 n3 t a1) /
fn a1 => fn a2 => case a1 of
| [g |- #p.3] => let [g |- #q.3] = a2 in [g |- #q.3]
| [g |- algeqapp (A1') B1] =>
  let [g |- algeqapp (A2') B2] = a2 in
  let [|- refltp] = algEqRUnique [g |- A1'] [g |- A2'] in
  let [g |- R1] = algEqRTrans [g |- A1'] [g |- A2'] in
  let [g |- R2] = algEqNTrans [g |- B1] [g |- B2] in
  [g |- algeqapp R1 R2]
| [g |- algeqproj1 A] =>
  let [g |- algeqproj1 A'] = a2 in
  let [|- refltp] = algEqRUnique [g |- A] [g |- A'] in
  let [g |- A''] = algEqRTrans [g |- A] [g |- A'] in
  [g |- algeqproj1 A'']
| [g |- algeqproj2 A] =>
  let [g |- algeqproj2 A'] = a2 in
  let [|- refltp] = algEqRUnique [g |- A] [g |- A'] in
  let [g |- A''] = algEqRTrans [g |- A] [g |- A'] in
  [g |- algeqproj2 A'']
| [g |- algeqbinp A1 A2] =>
  let [g |- algeqbinp A1' A2'] = a2 in
  let [|- refltp] = algEqRUnique [g |- A1] [g |- A1'] in
  let [|- refltp] = algEqRUnique [g |- A2] [g |- A2'] in
  let [g |- A1''] = algEqRTrans [g |- A1] [g |- A1'] in
  let [g |- A2''] = algEqRTrans [g |- A2] [g |- A2'] in
  [g |- algeqbinp A1'' A2'']
| [g |- algeqnamep] => let [g |- algeqnamep] = a2 in [g |- algeqnamep]
and algEqNTrans : (g:rctx)
  [g |- algeqn N1 N2 T]
-> [g |- algeqn N2 N3 T]
-> [g |- algeqn N1 N3 T] =
/ total a1 (algEqNTrans g n1 n2 n3 t a1) /
fn a1 => fn a2 => case a1 of
| [g |- algeqname S1 S2 (A1')] =>
  let [g |- algeqname (S2') S3 (A2')] = a2 in
  let [g |- refltm] = determinacy' [g |- S2] [g |- S2'] [g |- A1'] [g |- A2'] in
  let [g |- R] = algEqRTrans [g |- A1'] [g |- A2'] in
  [g |- algeqname S1 S3 R]
| [g |- algeqarr (\x.\u.A1')] =>
  let [g |- algeqarr \x.\u.A2'] = a2 in
  let (a2 : [g |- algeqn _ _ (arr T[] S[])]) = a2 in
  let [g,b:block x:tm,xt:oft x _,xx:algeqr x x _ |- R[..,b.1,b.3]]
      = algEqNTrans [g,b:block x:tm,xt:oft x T[],xx:algeqr x x T[] |- A1'[..,b.1,b.3]]
  		     [g,b:block x:tm,xt:oft x T[],xx:algeqr x x T[] |- A2'[..,b.1,b.3]] in
  [g |- algeqarr (\x.\u.R)]
| [g |- algequnit] => [g |- algequnit]
| [g |- algeqprod A1 A2] =>
  let [g |- algeqprod A1' A2'] = a2 in
  let [g |- A1''] = algEqNTrans [g |- A1] [g |- A1'] in
  let [g |- A2''] = algEqNTrans [g |- A2] [g |- A2'] in
  [g |- algeqprod A1'' A2'']
;

% % Transitivity of logical equivalence
rec logEqTrans : {T:[|- tp]}
    Log [g |- M sim N] [ |- T]
 -> Log [g |- N sim P] [ |- T]
 -> Log [g |- M sim P] [ |- T] =
/ total t (logEqTrans g m n p t) /
mlam T => fn e1 => fn e2 => case [|- T] of
| [|- tp/nm] =>
  let LogName a1 = e1 in
  let LogName a2 = e2 in LogName (algEqNTrans a1 a2)
| [|- arr T1 T2] =>
  let LogArr [g |-  M] [g |-  N] f1 = e1 in
  let LogArr [g |-  N] [g |-  P] f2 = e2 in
  LogArr [g |-  M] [g |-  P] (mlam h,$W,V1,D1,V2,D2 => fn v =>
   logEqTrans [|- T2] (f1 [h] [h |- $W] [h |-  V1] [h|- D1] [h |-  V2] [h |- D2] v)
                      (f2 [h] [h |- $W] [h |-  V2] [h|- D2] [h |-  V2] [h |- D2]
                          (logEqTrans [|- T1] (logEqSym [|- _ ] v) v)))
| [ |- tp/unit] => LogUnit
| [ |- prod T1 T2] =>
  let LogProd leq11 leq21 = e1 in
  let LogProd leq12 leq22 = e2 in
  let ih1 = logEqTrans [|- T1] leq11 leq12 in
  let ih2 = logEqTrans [|- T2] leq21 leq22 in
  LogProd ih1 ih2
;


deq : tm -> tm -> tp -> type.
d_beta : ({x:tm} oft x T -> deq (M2 x) (N2 x) S) -> deq M1 N1 T -> deq (app (lam (\x. M2 x)) M1) (N2 N1) S.
d_lam : ({x:tm} oft x T -> deq (M x) (N x) S) -> deq (lam (\x. M x)) (lam (\x. N x)) (arr T S).
d_ext : ({x:tm} oft x T -> deq (app M x) (app N x) S) -> deq M N (arr T S).
d_app : deq M1 M2 (arr T S) -> deq N1 N2 T -> deq (app M1 N1) (app M2 N2) S.
d_refl : oft M T -> deq M M T.
d_sym : deq M N T -> deq N M T.
d_trans : deq M N T -> deq N O T -> deq M O T.
d_pair : deq M1 N1 T1 -> deq M2 N2 T2 -> deq (pair M1 M2) (pair N1 N2) (prod T1 T2).
d_bin : deq M1 N1 tp/nm -> deq M2 N2 tp/nm -> deq (pair M1 M2) (pair N1 N2) tp/nm.


schema ctx = some [t:tp] block x:tm,y:oft x t;

% % Logical equivalence of substitutions. Written _|-s_simsim_:_ in the paper
inductive LogSub : {g0:ctx}(h:rctx){$S1: [h |- g0]}{$S2: [h |- g0]}
ctype  =
| Nil : LogSub [] [h |- ^] [h |- ^]
| Dot : LogSub [g0] [h |- $S1] [h |- $S2]
       -> Log [h |- M1 sim M2] [ |- R]
       -> {D1:[h |- oft M1 R[]]} {D2:[h |- oft M2 R[]]}
         LogSub [g0,b:block x:tm,y:oft x R[]] [h |- $S1, <M1;D1>] [h |- $S2, <M2;D2>]
;


% % Looking up a variable in logically equivalence substitutions gives a logical equivalence
% rec lookup : Lookup [|-G] [g |- X] [ |- T]
%  -> LogSub [g] [h |- $S1] [h |- $S2] [|- G]
%  -> Log [h |- X[$S1] sim X[$S2]] [ |- T] =
% / total v (lookup _ _ _ _ _ _ _ _ v) /
% fn v => fn s => case v of
% | Top =>    let Dot s' e = s in e
% | Pop v' => let Dot s' e = s in lookup v' s'
% ;

% Monotonicity of logical equivalence on substitutions
rec monotoneSub :
    {h0:rctx}{h:rctx}{$W:[h0 |- h]}
    LogSub [g] [h |- $S1] [h |- $S2]
  -> LogSub [g] [h0 |- $S1[$W]] [h0 |- $S2[$W]]=
 / total es (monotoneSub _ _ _ _ _ iv es ) /
mlam h0,h,$W => fn es => case es of
| Nil => Nil
| Dot rs' rN [h |- D1] [h |- D2]  => Dot (monotoneSub [h0] [h] [h0 |- $W] rs') (logEq_Monotone [h0] [h0 |- $W] rN) [h0 |- D1[$W]] [h0 |- D2[$W]]
;


% Symmetry of logical equivalence on substitutions
rec logEqSubSym :
   LogSub [g] [h |- $S1] [h |- $S2]
 -> LogSub [g] [h |- $S2] [h |- $S1] =
 / total e (logEqSubSym g h sigma sigma' e) /
fn e => case e of
| Nil => Nil
| Dot e' e1 [h |- D1] [h |- D2] => Dot (logEqSubSym e') (logEqSym  [|- _ ] e1) [h |- D2] [h |- D1];

% Transitivity of logical equivalence on substitutions
rec logEqSubTrans :
   LogSub [g] [h |- $S1] [h |- $S2]
 -> LogSub [g] [h |- $S2] [h |- $S3]
 -> LogSub [g] [h |- $S1] [h |- $S3] =
/ total e1 (logEqSubTrans g g h s1 s2 s3 e1) /
fn e1 => fn e2 => case e1 of
| Nil => let Nil = e2 in Nil
| Dot e1' x1 [h |- D1] [h |- D2] => let Dot e2' x2 [h |- D3] [h |- D4] = e2 in Dot (logEqSubTrans e1' e2') (logEqTrans [|- _ ] x1 x2) [h |- D1] [h |- D4];


rec lookup : {g:ctx}{#p:[g |- block (x:tm, u: oft x Q[])]} LogSub [g] [h |- $S1] [h |- $S2]
   -> Log [h |- #p.1[$S1] sim #p.1[$S2]] [|- Q] =
 / total g (lookup g) /
 mlam g, #p => fn s => case [g] of
	 | [] => impossible [ |- #p.1]
	 | [g',b:block x:tm,y:oft x S[]] =>
	   let Dot rs rm [h |- D1] [h |- D2] = s in
	   (case [g', b:block (x:tm, y:oft x S[]) |- #p.2] of
	      |  {#q:[g0 |- block (x:tm, u: oft x P[])]}
                 [g0, b:block (x:tm, y: oft x T[]) |- #q.2[..]] => lookup [g'] [g' |- #q] rs
	      | [g0, b:block (x:tm, u: oft x T[]) |- b.2] => rm
)
;

% Tiny fundamental theorem (Crary's fundamental theorem I)
rec thm' : [g |- oft M T[]] -> LogSub [g] [h |- $S1] [h |- $S2] -> Log [h |- M[$S1] sim M[$S2]] [|- T] =
/ total d (thm' g h m t s1 s2 d) /
fn d, s =>
let s : LogSub [g] [h |- $S1] [h |- $S2] = s in
case d of
| [g |- #p.2] => lookup [g] [g |- #p] s
| [g |- oft/app D1 D2] =>
  let LogArr [h |- M1] [h |- M2] f = thm' [g |- D1] s in
  f [h] [h |- ..] [h |- _] [h |- D2[$S1]] [h |- _] [h |- D2[$S2]] (thm' [g |- D2] s)
| [g |- oft/lam (\x.\y. D0)] =>
  let ([g |- _ ] : [g |- oft (lam \x.M) (arr P[] Q[])]) = d in
  LogArr [h |- lam \x.M[$S1[..],x]] [h |- lam \x.M[$S2[..],x]]
   (mlam h0,$W,N1,D1,N2,D2 => fn rn =>
    let q0 = Dot (monotoneSub [h0] [h] [h0 |- $W] s) rn [h0 |- D1] [h0 |- D2] in
    let q2 = thm' [g,b:block x:tm,y:oft x P[] |- D0[..,b.1,b.2]] q0 in
   closed [|- Q]
         [h0 |- trans1 step/beta refl]
         [h0 |- trans1 step/beta refl]
         q2)
| [g |- oft/nm] => LogName [h |- algeqname refl refl algeqnamep]
| [g |- oft/bin D1 D2] =>
  let ih1 = thm' [g |- D1] s in
  let ih2 = thm' [g |- D2] s in
  let LogName [h |- algeqname MS11 MS12 A1] = ih1 in
  let LogName [h |- algeqname MS21 MS22 A2] = ih2 in 
  let s1 = mstep_bin [h |- MS11] [h |- MS21] in 
  let s2 = mstep_bin [h |- MS12] [h |- MS22] in 
  let [h |- MS1] = s1 in ?

%  let [h |- MS1] = mstep_bin [h |- MS11] [h |- MS21] in ?
% 
%  let [h |- MS2] = mstep_bin [h |- MS12] [h |- MS22] in
%  let thing = [h |- algeqname MS1 MS2 ?] in
%  ?
| [g |- oft/pair D1 D2] =>
  let ih1 = thm' [g |- D1] s in
  let ih2 = thm' [g |- D2] s in
  LogProd ih1 ih2
;


% Perhaps an alternative to this is to explicitly carry around typing derivations in thm
rec dtp1 : (g:ctx) [g |- deq M1 M2 T[]] -> [g |- oft M1 T[]] =
/ total d (dtp1 g m1 m2 t d) /
fn d => case d of
| [g |- d_app D1 D2] =>
  let [g |- E1] = dtp1 [g |- D1] in
  let [g |- E2] = dtp1 [g |- D2] in
  [g |- oft/app E1 E2]
| [g |- d_lam (\x.\y. D1)] =>
  let [g,b:block x:tm,y:oft x T[] |- E1] = dtp1 [g,b:block x:tm,y:oft x _ |- D1[..,b.1,b.2]] in
  [g |- oft/lam (\x.\y. E1[..,<x;y>])]
| [g |- d_beta (\x.\y. D1) D2] =>
  let [g,b:block x:tm,y:oft x T[] |- E1] = dtp1 [g,b:block x:tm,y:oft x _ |- D1[..,b.1,b.2]] in
  let [g |- E2] = dtp1 [g |- D2] in
  [g |- oft/app (oft/lam (\x.\y. E1[..,<x;y>])) E2]
| [g |- d_ext (\x.\y. D1)] =>
  let [g,b:block x:tm,y:oft x T[] |- oft/app E1[..] b.2] = dtp1 [g,b:block x:tm,y:oft x _ |- D1[..,b.1,b.2]] in
  [g |- E1]
| [g |- d_refl D1] => [g |- D1]
| [g |- d_sym D1] => dtp2 [g |- D1]
| [g |- d_trans D1 D2] => dtp1 [g |- D1]
| todo => ?
and dtp2 : (g:ctx) [g |- deq M1 M2 T[]] -> [g |- oft M2 T[]] =
 / total d (dtp2 g m1 m2 t d) /
fn d => case d of
| [g |- d_app D1 D2] =>
  let [g |- E1] = dtp2 [g |- D1] in
  let [g |- E2] = dtp2 [g |- D2] in
  [g |- oft/app E1 E2]
| [g |- d_lam (\x.\y. D1)] =>
  let [g,b:block x:tm,y:oft x T[] |- E1] = dtp2 [g,b:block x:tm,y:oft x _ |- D1[..,b.1,b.2]] in
  [g |- oft/lam (\x.\y. E1[..,<x;y>])]
| [g |- d_beta (\x.\y. D1) D2] =>
  let [g,b:block x:tm,y:oft x T[] |- E1] = dtp2 [g,b:block x:tm,y:oft x _ |- D1[..,b.1,b.2]] in
  let [g |- E2] = dtp2 [g |- D2] in
  [g |- E1[..,< _ ; E2 >]]
| [g |- d_ext (\x.\y. D1)] =>
  let [g,b:block x:tm,y:oft x T[] |- oft/app E1[..] b.2] = dtp2 [g,b:block x:tm,y:oft x _ |- D1[..,b.1,b.2]] in
  [g |- E1]
| [g |- d_refl D1] => [g |- D1]
| [g |- d_sym D1] => dtp1 [g |- D1]
| [g |- d_trans D1 D2] => dtp2 [g |- D2]
| todo => ?
;

% % Fundamental theorem
rec thm : [g |- deq M1 M2 T[]]
        -> LogSub [g] [h |- $S1] [h |- $S2]
        -> Log [h |- M1[$S1] sim M2[$S2]] [|- T] =
 / total d (thm g h m1 m2 t sigma sigma' d) /
fn d , s =>
let s : LogSub [g] [h |- $S1] [h |- $S2] = s in
case d of
| [g |- d_app D1 D2] =>
  let LogArr [h |- M1] [h |- M2] f = thm [g |- D1] s in
  let [g |- E1] = dtp1 [g |- D2] in
  let [g |- E2] = dtp2 [g |- D2] in
  f [h] [h |- ..] [h |- _] [h |- E1[$S1]] [h |- _] [h |- E2[$S2]] (thm [g |- D2] s)
| [g |- d_beta (\x.\y. D1) D2] =>
  let [h |- E1] = dtp1 [h |- D2[$S1]] in
  let [h |- E2] = dtp2 [h |- D2[$S2]] in
   let q0 = thm [g,b:block x:tm,y:oft x _ |- D1[..,b.1,b.2]] (Dot s (thm [g |- D2] s)  [h |- E1] [h |- E2]) in
   closed [|- _] [h |- trans1 step/beta refl] [h |- refl] q0
| [g |- d_lam \x.\y. E1] =>
  let ([g |- _ ] : [g |- deq (lam \x.M1) (lam \x.M2) (arr P[] Q[])]) = d in
  LogArr [h |- lam (\x. M1[$S1[..],x])] [h |- lam (\x. M2[$S2[..],x])]
   (mlam h0,$W,N1,D1,N2,D2 => fn rn =>
    let q0 = Dot (monotoneSub [h0] [h] [h0 |- $W] s) rn [h0 |- D1] [h0 |- D2] in
    let q2 = thm [g,b:block x:tm,y:oft x _ |- E1[..,b.1,b.2]] q0 in
    closed [|- Q] [h0 |- trans1 step/beta refl] [h0 |- trans1 step/beta refl] q2
   )
| [g |- d_ext (\x.\y. D0)] =>
  let ([g |- _ ] : [g |- deq M1 M2 (arr P[] Q[])]) = d in
  LogArr [h |- M1[$S1]] [h |- M2[$S2]] (mlam h0,$W,N1,D1,N2,D2 => fn rn =>
    let q0 = Dot (monotoneSub [h0] [h] [h0 |- $W] s) rn [h0 |- D1] [h0 |- D2] in
    thm [g,b:block x:tm,y:oft x _ |- D0[..,b.1,b.2]] q0
  )
| [g |- d_refl D1] => thm' [g |- D1] s
| [g |- d_sym D1] => logEqSym [|- _] (thm [g |- D1] (logEqSubSym s))
| [g |- d_trans D1 D2] => logEqTrans [|- _] (thm [g |- D1] s) (thm [g |- D2] (logEqSubTrans (logEqSubSym s) s))
| [g |- d_pair D1 D2] => ?
| [g |- d_bin D1 D2] => ?
;

% $Id is a substitution which moves from a term context to a neutral term context in the obvious way
inductive IdSub : (g':rctx){g:ctx}{$Id:[g' |- g]} ctype =
| INil : IdSub [] [|- ^]
| ISnoc : {T:[|-tp]}IdSub [g] [g' |- $Id]
        -> IdSub [g,b:block (x:tm, u:oft x T[])] [g',b:block (x:tm, u:oft x T[], a:algeqr x x T[]) |- $Id[..], <b.1 ; b.2> ]
;


rec idLogSub : IdSub [g] [g' |- $Id] -> LogSub [g] [g' |- $Id] [g' |- $Id] =
/ total r (idLogSub g g s r) /
fn r => case r of
| INil => Nil
| ISnoc [|- T] r' =>
 let (s : LogSub [g] [h |- $S] [h |- $S]) = idLogSub r' in
  let wkn = monotoneSub
       [h,b:block (x:tm,u: oft x T[], a:algeqr x x T[]) ] [h]
       [h,b:block (x:tm,u: oft x T[], a:algeqr x x T[]) |- ..] s in
  let rx = reflect [|- T] [h,b:block (x:tm , u:oft x T[], a: algeqr x x T[]) |- b.3] in
   Dot wkn rx  [h,b:block (x:tm,u: oft x T[], a:algeqr x x T[]) |- b.2]
               [h,b:block (x:tm,u: oft x T[], a:algeqr x x T[]) |- b.2]
;


rec main : {T:[ |- tp]}[g |- deq M N T[]] -> IdSub [g] [h |- $S] -> [h |- algeqn M[$S] N[$S] T[]] =
mlam T => fn d => fn wk => reify [|- T] (thm d (idLogSub wk)) ;

