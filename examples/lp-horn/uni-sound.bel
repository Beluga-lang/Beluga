% Soundness of uniform proofs
% Author: Brigitte Pientka
% Adapted from Twelf proof

schema ctx = some [a:o] block u: assume a , _t:pf a + i ;

rec s_sound : {g:ctx}[g |- solve A] -> [g |- pf A] =
/ total s (s_sound g a s)/
mlam g => fn s => case s of
| [g |- s_and S2 S1] =>
  let [g |- D1] = s_sound [g] [g |- S1] in
  let [g |- D2] = s_sound [g] [g |- S2] in
    [g |- andi D2 D1]

| [g |- s_true] => [g |- truei]

| [g |- s_atom S (#p.1)] =>
     let [g,u:pf _  |-  D] = i_sound [g] [g |- S] in
     [g |- D[..,(#p.2)]]

and rec i_sound : {g:ctx}[g |- focus A P] -> [g, u:pf A |-  pf (atom (P[..]))] =
/ total f (i_sound g a p f)/
mlam g => fn f => case f of
| [g |- i_andl I1] =>
  let [g,u:pf _  |-  D1] = i_sound [g] [g |- I1] in
    [g, u |-  D1[..,(andel u)]]
| [g |- i_andr I2] =>
  let [g,u:pf _  |-  D2] = i_sound [g] [g |- I2] in
    [g, u |-  D2[..,(ander u)]]

| [g |- i_atom] => [g,u |- u]

| [g |- i_imp S2 I1] =>
    let [g,u:pf _  |-  D1] = i_sound [g] [g |- I1] in
    let [g |- D2] = s_sound [g] [g |- S2] in
     [g,u |- D1[..,(impe u (D2[..]))]]

| [g |- i_forall T I1] =>
    let [g,u: pf _  |-  D1] = i_sound [g] [g |- I1] in
      [g,u |- D1[..,(foralle u (T[..]))]]
;

%
% Final theorem for closed derivations
% If . |- solve A    then    . |- pf A  .
%
rec ss_sound : [ |- solve A] -> [ |- pf A] =
 fn d =>  s_sound [ ] d ;
