% Variable counting (see Pientka [POPL'08])
% Author:  Brigitte Pientka
%
% This example only uses weak higher-order abstract syntax.

tp  : type.
nat : tp.
bool: tp.

exp : tp -> type.
z   : exp nat.
s   : exp nat -> exp nat.

tt  : exp bool.

add : exp nat -> exp nat -> exp nat.
letv: {T1:tp} {T2:tp} exp T1 -> (exp T1 -> exp T2) -> exp T2.

schema W = some [T:tp] block exp T;


rec plus : (exp nat) [.] -> (exp nat) [.] -> (exp nat) [.] =
fn x => fn y =>
 (case x of
    box(. . z)                           : (exp nat)[.] => y

  | {U::(exp nat)[.]} box(. . s U ![.]!) : (exp nat)[.] =>
    (case (plus box(. U![.]!) y) of
       {V::(exp nat)[.]} box(. . V![.]!) : (exp nat)[.] 
       => box(. s V![.]!)));


rec cntV : {g:(W)*} {T::tp[.]} {S::tp[.]} (exp S![.]!)[g, x:(exp T![.]!)] -> (exp nat) [.] =
FN g => mlam t => mlam tt => fn e =>
  (case e of
     {T1::tp[.]} {T2::tp[.]} {#p::(exp T1![.]!)[g]}
     box(g,x: exp T2![.]! . #p![id]!)      : (exp T1![.]!)[g,x: exp T2![.]!] => box(. z)

   | {T'::tp[.]} box(g,x: exp T'![.]! . x)  : (exp T'![.]!)[g,x: exp T'![.]!] => box(. s z)

   | {T'::tp[.]} box(g,x: exp T'![.]! . tt) : (exp bool) [g,x: exp T'![.]!] => box(. z)

   | {T'::tp[.]} box(g,x: exp T'![.]! . z)  : (exp nat)  [g,x: exp T'![.]!] => box(. z)

   | {T'::tp[.]} {U::(exp nat)[g, x:(exp T'![.]!)]}
      box(g,x: exp T'![.]! . s U ![id, x]! )  : (exp nat) [g,x: exp T'![.]!] => 
          cntV [g] <. T'![.]! >  < . nat >   box(g,x . U![id, x]!)

   | {T'::tp[.]} {T1::tp[.]} {T2::tp[.]} 
     {U1::(exp T1![.]!)[g, x:(exp T'![.]!)]} {U2::(exp T2![.]!)[g, x:(exp T'![.]!), y:(exp T1![.]!)]}

      box(g,x: exp T'![.]! . letv T1![.]! T2![.]! U1![id, x]! (\ y . U2![id, x, y]!)) : (exp T2![.]!)[g, x:(exp T'![.]!)] =>

          plus (cntV [g] <. T'![.]! >     < . T1![.]! >    box(g,x . U1![id, x]!)) 
               (cntV [g, y:(exp T1![.]!)] < . T'![.]! >    < . T2![.]! >   box(g,y,x . U2![id, x, y]!))

        | {T'::tp[.]} {U::(exp nat)[g, x:(exp T'![.]!)]} {W::(exp nat)[g, x:(exp T'![.]!)]}
          box(g,x : exp T1![.]! . add U![id, x]! W![id, x]!) : (exp nat)[g, x: exp T'![.]!] =>
          plus (cntV [g] <. T'![.]!> < . nat > box(g,x . U![id, x]!))
               (cntV [g] <. T'![.]!> < . nat > box(g,x . W![id, x]!))

);

