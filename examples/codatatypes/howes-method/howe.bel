
% This version uses current Beluga syntax and thus compiles.
% It uses applicative simulation but substitutivity is defined in simultaneous style.


tp : type.
top : tp.
arr : tp -> tp -> tp.
list : tp -> tp.

exp : tp -> type.
app : exp (arr S T) -> exp S -> exp T.
lam : (exp S -> exp T) -> exp (arr S T).
fix : (exp T -> exp T) -> exp T.
unit : exp top.
nil : exp (list T).
cons : exp T -> exp (list T) -> exp (list T).
lcase : exp (list S) -> exp T -> (exp S -> exp (list S) -> exp T) -> exp T.

value : exp T -> type.
val-lam : value (lam (\x.E x)).
val-unit : value unit.
val-nil : value nil.
val-cons : value (cons E1 E2).

eval : exp T -> exp T -> type.
ev-app : eval E1 (lam (\x.E x)) -> eval (E E2) V -> eval (app E1 E2) V.
ev-val : value V -> eval V V.
ev-fix : {E:exp T -> exp T} eval (E (fix (\x.E x))) V ->  eval (fix (\x.E x)) V.
ev-case-nil : eval E nil -> eval E1 V -> eval (lcase E E1 (\h.\t.E2 h t)) V.
ev-case-cons : {E2: exp S -> exp (list S) -> exp T} eval E (cons EH ET) ->  eval (E2 EH ET) V
             -> eval (lcase E E1 (\h.\t.E2 h t)) V.

schema ctx = some [t:tp] exp t;

% ICFP'16 uses postfix syntax. Current Beluga syntax uses prefix syntax.
% Maybe we will want to change that to match postfix style
coinductive Sim : {T:[|- tp]} [|- exp T] -> [|- exp T] -> ctype =
|  (Sim_lam : Sim [|- arr S T] [|- E1] [|- E2])
          :: [|- eval E1 (lam (\x.E1'))] -> ExSim [x:exp S |- E1'] [|- E2]
| Sim_top : Sim [|- top] [|- E1] [|- E2] :: [|- eval E1 unit] -> [|- eval E2 unit]
| Sim_nil : Sim [|- list T] [|- E1] [|- E2] :: [|- eval E1 nil] -> [|- eval E2 nil]
| Sim_cons : Sim [|- list T] [|- E1] [|- E2]
             :: [|- eval E1 (cons EH ET)] -> ExSimCons [|- EH] [|- ET] [|- E2]

and inductive ExSim : [x:exp S |- exp T[]] -> [|- exp (arr S T)] -> ctype =
| ExSimlam : [|- eval E2 (lam (\x.E2'))]
             -> ({E:[|- exp S]} Sim [|- T] [|- E1'[E]] [|- E2'[E]])
                 -> ExSim [x:exp S |- E1'] [|- E2]

and inductive ExSimCons : [|- exp T] -> [|- exp (list T)] -> [|- exp (list T)] -> ctype =
| ExSimcons : [|- eval E2 (cons EH' ET')] -> Sim [|- T] [|- EH] [|- EH']
              -> Sim [|- list T] [|- ET] [|- ET']
              -> ExSimCons [|- EH] [|- ET] [|- E2]
;


rec sim_refl : {E:[|- exp T]} Sim [|- T] [|- E] [|- E] =
fun [|- E1] .Sim_lam (d : [|- eval E1 (lam \x.E1')]) =>
   ExSimlam d (mlam E' => sim_refl [|- E1'[E']])
  | [|- E] .Sim_top d => d
  | [|- E] .Sim_nil d => d
  | [|- E] .Sim_cons (d : [|- eval E (cons H T)]) =>
    ExSimcons d (sim_refl [|- H]) (sim_refl [|- T])
;

rec sim_trans : Sim [|- T] [|- E1] [|- E2] -> Sim [|- T] [|- E2] [|- E3]
                -> Sim [|- T] [|- E1] [|- E3] =
fun s1 s2 .Sim_lam d =>
    let ExSimlam d1 s3 = Sim_lam s1 d in
    let ExSimlam d2 s4 = Sim_lam s2 d1 in
    ExSimlam d2 (mlam E => sim_trans (s3 [|- E]) (s4 [|- E]))
  | s1 s2 .Sim_top d =>
    Sim_top s2 (Sim_top s1 d)
  | s1 s2 .Sim_nil d => Sim_nil s2 (Sim_nil s1 d)
  | s1 s2 .Sim_cons d =>
    let ExSimcons d1 s1' s1'' = Sim_cons s1 d in
    let ExSimcons d2 s2' s2'' = Sim_cons s2 d1 in
    ExSimcons d2 (sim_trans s1' s2') (sim_trans s1'' s2'')
;

inductive OSim : (g:ctx) {T:[|- tp]} [g |- exp T[]] -> [g |- exp T[]] -> ctype =
  | OSimC : {g:ctx}({#S:[ |- g]} Sim [|- T] [|- E1 [#S]] [|- E2 [#S]])
               -> OSim [|- T] [g |- E1] [g |- E2]
;

rec sim_osim : Sim [|- T] [|- E1] [|- E2] -> OSim [|- T] [|- E1] [|- E2] =
 fn s => OSimC [] (mlam #S => let [ |- ^ ] = [ |- #S] in s)
;

rec osim_refl : (g:ctx) {E:[g |- exp T[]]} OSim [|- T] [g |- E] [g |- E] =
   mlam E => OSimC [ _ ] (mlam #S => sim_refl [|- E[#S]])
;

rec osim_trans :  (g:ctx) OSim [|- T] [g |- E1] [g |- E2] -> OSim [|- T] [g |- E2] [g |- E3]
                  -> OSim [|- T] [g |- E1] [g |- E3] =
fun (OSimC [g] f1) (OSimC [g] f2) =>
    OSimC [g] (mlam #S => sim_trans (f1 [|- #S]) (f2 [|- #S]))
;


rec osim_cus : (g:ctx) (h:ctx) {#S:[h |- g]} OSim [|- T] [g |- E1] [g |- E2]
               -> OSim [|- T] [h |- E1[#S]] [h |- E2[#S]] =
    fun [h |- #S] (OSimC [g] f) => OSimC [h] (mlam #S' => f [|- #S[#S']])
;


inductive Howe : (g:ctx) {T:[|- tp]} [g |- exp T[]] -> [g |- exp T[]] -> ctype =
  | Howe_unit : OSim [|- top] [g |- unit] [g |- E] -> Howe [|- top] [g |- unit] [g |- E]
  | Howe_var : {#p:[g |- exp T[]]}
                OSim [|- T] [g |- #p] [g |- E]
               -> Howe [|- T] [g |- #p] [g |- E]
  | Howe_lam : Howe [|- T] [g,x:exp _ |- E1] [g,x:exp _ |- E2]
               -> OSim [|- arr S T] [g |- lam (\x.E2)] [g |- E]
               -> Howe [|- arr S T] [g |- lam (\x.E1)] [g |- E]
  | Howe_app : Howe [|- arr S T] [g |- E1] [g |- E1'] -> Howe [|- S] [g |- E2] [g |- E2']
               -> OSim [|- T] [g |- app E1' E2'] [g |- E]
               -> Howe [|- T] [g |- app E1 E2] [g |- E]
  | Howe_fix : Howe [|- T] [g,x:exp T[] |- E1] [g,x:exp T[] |- E2]
               -> OSim [|- T] [g|- fix (\x.E2)] [g |- E]
               -> Howe [|- T] [g |- fix (\x.E1)] [g |- E]
  | Howe_nil : OSim [|- list T] [g |- nil] [g |- E]
               -> Howe [|- list T] [g |- nil] [g |- E]
  | Howe_cons : Howe [|- T] [g |- E1] [g |- E1']
               -> Howe [|- list T] [g |- E2] [g |- E2']
               -> OSim [|- list T] [g |- cons E1' E2'] [g |- E]
               -> Howe [|- list T] [g |- cons E1 E2] [g |- E]
  | Howe_case : Howe [|- list T] [g |- E] [g |- E']
               -> Howe [|- T'] [g |- E1] [g |- E1']
               -> Howe [|- T'] [g,h:exp T[],t:exp (list T[]) |- E2]
                               [g,h:exp T[],t:exp (list T[]) |- E2']
               -> OSim [|- T'] [g |- lcase E' E1' (\h.\t.E2')] [g |- E'']
               -> Howe [|- T'] [g |- lcase E E1 (\h.\t.E2)] [g |- E'']
;


rec howe_osim_trans : (g:ctx) Howe [|- T] [g |- E1] [g |- E2]
                      -> OSim [|- T] [g |- E2] [g |- E3]
                      -> Howe [|- T] [g |- E1] [g |- E3] =
fun (Howe_unit s1) s2 => Howe_unit (osim_trans s1 s2)
  | (Howe_var [g |- #p] s1) s2 => Howe_var [g |- #p] (osim_trans s1 s2)
  | (Howe_lam h1 s1) s2 => Howe_lam h1 (osim_trans s1 s2)
  | (Howe_app h1 h2 s1) s2 => Howe_app h1 h2 (osim_trans s1 s2)
  | (Howe_fix h1 s1) s2 => Howe_fix h1 (osim_trans s1 s2)
  | (Howe_nil s1) s2 => Howe_nil (osim_trans s1 s2)
  | (Howe_cons h1 h2 s1) s2 => Howe_cons h1 h2 (osim_trans s1 s2)
  | (Howe_case h1 h2 h3 s1) s2 => Howe_case h1 h2 h3 (osim_trans s1 s2)
;


rec howe_refl : (g:ctx) {E:[g |- exp T[]]} Howe [|- T] [g |- E] [g |- E] =
fun [g |- unit] => Howe_unit (osim_refl [g |- unit])
  | [g |- #p] => Howe_var [g |- #p] (osim_refl [g |- #p])
  | [g |- lam \x.E] => Howe_lam (howe_refl [g,x:exp _ |- E]) (osim_refl [g |- lam \x.E])
  | [g |- app E1 E2] => Howe_app (howe_refl [g |- E1]) (howe_refl [g |- E2]) (osim_refl [g |- app E1 E2])
  | [g |- fix \x.E] => Howe_fix (howe_refl [g,x:exp _ |- E]) (osim_refl [g |- fix \x.E])
  | [g |- nil] => Howe_nil (osim_refl [g |- nil])
  | [g |- cons E1 E2] => Howe_cons (howe_refl [g |- E1]) (howe_refl [g |- E2]) (osim_refl [g |- cons E1 E2])
  | [g |- lcase E E1 (\h.\t.E2)] =>
    Howe_case (howe_refl [g |- E]) (howe_refl [g |- E1]) (howe_refl [g,h:exp _, t:exp (list _) |- E2])
              (osim_refl [g |- lcase E E1 (\h.\t.E2)])
;

rec osim_howe : (g:ctx) OSim [|- T] [g |- E1] [g |- E2] -> Howe [|- T] [g |- E1] [g |- E2] =
    fun s : OSim [|- T] [g |- E1] [g |- E2] =>
            howe_osim_trans (howe_refl [g |- E1]) s
;

rec sim_howe : Sim [|- T] [|- E1] [|- E2] -> Howe [|- T] [|- E1] [|- E2] =
    fun s => osim_howe (sim_osim s)
;

inductive HoweSubst : {g:ctx} (h:ctx) {#S1 : [h |- g]} {#S2 : [h |- g]} ctype =
  | HNil : HoweSubst [] [h |- ^] [h |- ^]
  | HCons : HoweSubst [g] [h |- #S1] [h |- #S2]
            -> Howe [|- T] [h |- E1] [h |- E2]
            -> HoweSubst [g,x:exp T[]] [h |- #S1, E1] [h |- #S2, E2]
;

rec howeSubst_refl : {g:ctx} {h:ctx} {#S:[h |- g]} HoweSubst [g] [h |- #S] [h |- #S] =
    mlam g, h, #S => case [h |- #S] of
| [h |- ^] => HNil
| {h:ctx}{g:ctx}{T:[ |- tp]}{#S': [h |- g]}{E:[h |- exp T[]]}
  [h |- #S', E] : $[h |- g, x:exp T[]] =>
  let hs = howeSubst_refl [g] [h] [h |- #S'] in
  let h  = howe_refl [h |- E] in
   HCons hs h
;

rec howe_ren : {g:ctx}{h:ctx}{#S: [h |-# g]} Howe [|- T] [g |- E1] [g |- E2]
                -> Howe [|- T] [h |- E1[#S]] [h |- E2[#S]] =
 mlam g, h, #S => fn hr => case hr of
| Howe_unit s => Howe_unit (osim_cus [h |- #S] s)
| Howe_var [g |- #p] s =>
      let os = osim_cus [h |- #S] s in
      howe_osim_trans (howe_refl [h |- #p[#S]]) os
| Howe_lam h s => Howe_lam (howe_ren [g,x:exp _] [h,x:exp _] [h,x:exp _ |- #S[..],x] h) (osim_cus [h |- #S] s)
| Howe_app h1 h2 s => Howe_app (howe_ren [g] [h] [h |- #S] h1) (howe_ren [g] [h] [h |- #S] h2) (osim_cus [h |- #S] s)
| Howe_fix h s => Howe_fix (howe_ren [g,x:exp _] [h,x:exp _] [h,x:exp _ |- #S[..],x] h) (osim_cus [h |- #S] s)
| Howe_nil s => Howe_nil (osim_cus [h |- #S] s)
| Howe_cons h1 h2 s => Howe_cons (howe_ren [g] [h] [h |- #S] h1) (howe_ren [g] [h] [h |- #S] h2) (osim_cus [h |- #S] s)
| Howe_case h1 h2 h3 s => Howe_case (howe_ren [g] [h] [h |- #S] h1) (howe_ren [g] [h] [h |- #S] h2)
                                    (howe_ren [g,x:exp _,y:exp _] [h,x:exp _,y:exp _] [h,x:exp _, y:exp _ |- #S[..],x,y] h3)
                                    (osim_cus [h |- #S] s)
;


rec howeSubst_wkn : (g:ctx) (h:ctx) {S:[|-tp]} HoweSubst [g] [h |- #S1] [h |- #S2]
                    -> HoweSubst [g] [h,x:exp S[] |- #S1[..]] [h,x:exp S[] |- #S2[..]] =
fun [|- S] HNil => HNil
  | [|- S] HCons (hs':HoweSubst [g] [h |- #S1] [h |- #S2]) h' =>
    HCons (howeSubst_wkn [|- S] hs')  (howe_ren [h] [h, x:exp S[] ] [h, x:exp S[] |- .. ] h')
;

rec howe_subst_var : (g:ctx) (h:ctx) Howe [|- T] [g |- #p] [g |- E2]
                     -> HoweSubst [g] [h |- #S1] [h |- #S2]
                     -> Howe [|- T] [h |- #p[#S1]] [h |- E2[#S2]] =
    fn h => fn hs =>
    let hs:HoweSubst [g] [h |- #S1] [h |- #S2] = hs in
    case h of
    | Howe_var [g,x:exp T[] |- x] s =>
      let HCons hs' h' = hs in
      howe_osim_trans h' (osim_cus [h |- #S2] s)
    | Howe_var [g,x:exp T[] |- #p[..]] s =>
      let HCons (hs':HoweSubst [g] [h |- #S1'] [h |- #S2']) h' = hs in
      howe_osim_trans (howe_subst_var (howe_refl [g |- #p]) hs')
                      (osim_cus [h |- #S2] s)
;

rec howe_subst : (g:ctx) (h:ctx) Howe [|- T] [g |- E1] [g |- E2]
                 -> HoweSubst [g] [h |- #S1] [h |- #S2]
                 -> Howe [|- T] [h |- E1[#S1]] [h |- E2[#S2]] =
    fn h => fn hs =>
    let hs:HoweSubst [g] [h |- #S1] [h |- #S2] = hs in
    case h of
    | Howe_unit s => howe_osim_trans (howe_refl [h |- _]) (osim_cus [h |- #S2] s)
    | Howe_var [g |- #p] s => howe_subst_var h hs
    | Howe_lam h' s =>
      Howe_lam (howe_subst h' (HCons (howeSubst_wkn [|- _] hs) (howe_refl [h,x:exp _ |- x])))
               (osim_cus [h |- #S2] s)
    | Howe_app h1' h2' s =>
      Howe_app (howe_subst h1' hs) (howe_subst h2' hs) (osim_cus [h |- #S2] s)
    | Howe_fix h' s =>
      Howe_fix (howe_subst h' (HCons (howeSubst_wkn [|- _] hs) (howe_refl [h,x:exp _ |- x])))
               (osim_cus [h |- #S2] s)
    | Howe_nil s => howe_osim_trans (howe_refl [h |- _]) (osim_cus [h |- #S2] s)
    | Howe_cons h1' h2' s =>
      Howe_cons (howe_subst h1' hs) (howe_subst h2' hs) (osim_cus [h |- #S2] s)
    | Howe_case h' h1' h2' s =>
      Howe_case (howe_subst h' hs) (howe_subst h1' hs)
                (howe_subst h2'
                      (HCons (HCons (howeSubst_wkn [|- _] (howeSubst_wkn [|- _] hs))
                             (howe_refl [h,x:exp _, y:exp _ |- x]))
                          (howe_refl [h,x:exp _, y:exp _ |- y])))
                (osim_cus [h |- #S2] s)
;

% Datatype to pack an existential
inductive HoweAbs : [x:exp S |- exp T[]] -> [|- exp (arr S T)] -> ctype =
  | HoweAbsC : [|- eval E2 (lam \x.E2')]
               -> ({E:[|- exp S]} Howe [|- T][|- E1' [E]] [|- E2' [E]])
               -> HoweAbs [x:exp S |- E1'] [|- E2]
;

rec howe_ev_abs : Howe [ |- arr S T] [|- lam (\x.E1')] [|- E2]
                  -> HoweAbs [x:exp S |- E1'] [|- E2] =
fun Howe_lam h' (OSimC [ _ ] s) =>
    let ExSimlam [|- D] f = Sim_lam (s [|- ^]) [|- ev-val val-lam] in
    HoweAbsC [|- D] (mlam E => let a = sim_osim (f [|- E]) in
                               let h'' = howe_subst h' (HCons HNil (howe_refl [|- E])) in
                                howe_osim_trans h'' a)
;

rec howe_ev_unit : Howe [ |- top] [ |- unit] [ |- N ] -> [ |- eval N unit] =
fun Howe_unit (OSimC [_ ]f) => Sim_top (f [|- ^]) [ |- ev-val val-unit]
;

rec howe_ev_nil :  Howe [ |- list T] [ |- nil] [ |- N ] -> [ |- eval N nil] =
fun Howe_nil (OSimC [ _ ] f) => Sim_nil (f [|- ^]) [ |- ev-val val-nil]
;

% Datatype to pack an existential
inductive HoweCons : [|- exp T] -> [|- exp (list T)] -> [|- exp (list T)] -> ctype =
  | HoweConsC : [|- eval N (cons N' NS')] -> Howe [|- T] [|- M'] [|- N']
             -> Howe [|- list T][|- MS'] [|- NS'] -> HoweCons [|- M'] [|- MS'] [|- N]
;

rec howe_ev_cons : Howe [ |- list T] [ |- cons M MS] [ |- N] -> HoweCons [|- M] [|- MS] [|- N] =
fun Howe_cons h1 h2 (OSimC [ _ ] s) =>
    let ExSimcons [|- D] s1 s2 = Sim_cons (s [|- ^]) [|- ev-val val-cons] in
    HoweConsC [|- D] (howe_osim_trans h1 (sim_osim s1)) (howe_osim_trans h2 (sim_osim s2))
;

rec abs_sim_trans : {E1:[x:exp S|- exp T[]]}{E2:[x:exp S|- exp T[]]}{E3:[x:exp S|- exp T[]]}
                       ({E:[|- exp S]} Sim [|- T] [|- E1[E]] [|- E2[E]])
                    -> ({E:[|- exp S]} Sim [|- T] [|- E2[E]] [|- E3[E]])
                    -> ({E:[|- exp S]} Sim [|- T] [|- E1[E]] [|- E3[E]]) =
fun [x:exp S |- E1] [x:exp S |- E2] [x:exp S |- E3] e1 e2 [|- E] => sim_trans (e1 [|- E]) (e2 [|- E])
;

rec sim_lam : {E1:[x:exp S |- exp T[]]}{E2:[x:exp S |- exp T[]]}
              ({E:[|- exp S]} Sim [|- T] [|- E1[E]] [|- E2[E]])
              -> Sim [|- arr S T] [|- lam \x.E1] [|- lam \x.E2] =
fun [x:exp S |- E1] [x:exp S |- E2] s .Sim_lam [|- ev-val val-lam] => ExSimlam [|- ev-val val-lam] s
;

aeq : exp T -> exp T -> type.
refl: aeq E E.

rec ev_value : [|- eval E V] -> [|- value V] =
fun [|- ev-app D1 D2] => ev_value [|- D2]
  | [|- ev-val V] => [|- V]
  | [|- ev-fix (\x.M) D] => ev_value [|- D]
  | [|- ev-case-nil D D1] => ev_value [|- D1]
  | [|- ev-case-cons (\h.\t.D) D1 D2] => ev_value [|- D2]
;

rec ev_det : [|- eval E V1] -> [|- eval E V2] -> [|- aeq V1 V2] =
fun [|- ev-app M1 M2] [|- ev-app N1 N2] =>
    let [|- refl] = ev_det [|- M1] [|- N1] in
    let [|- refl] = ev_det [|- M2] [|- N2] in
    [|- refl]
  | [|- ev-val V1] [|- ev-val V2] => [|- refl]
  | [|- ev-fix (\x.M) D1] [|- ev-fix (\x.M) D2] =>
    let [|- refl] = ev_det [|- D1] [|- D2] in
    [|- refl]
  | [|- ev-case-nil M1 M2] [|- ev-case-nil N1 N2] =>
    let [|- refl] = ev_det [|- M2] [|- N2] in
    [|- refl]
  | [|- ev-case-cons (\x.\y.M) D1 D2] [|- ev-case-cons (\x.\y.M) D1' D2'] =>
    let [|- refl] = ev_det [|- D1] [|- D1'] in
    let [|- refl] = ev_det [|- D2] [|- D2'] in
    [|- refl]
;

rec ev_val : [|- eval E V] -> [|- eval V V'] -> [|- aeq V V'] =
fun [|- D] [|- D'] =>
    let [|- V] = ev_value [|- D] in
    ev_det [|- ev-val V] [|- D']
;

rec ev_sim' : [|- eval E2 (lam \x.F)] -> Sim [|- arr S T] [|- E1] [|- E2]
              -> Sim [|- arr S T] [|- E1] [|- lam \x.F] =
fun [|- D] s .Sim_lam e => let ExSimlam [|- D'] s' = Sim_lam s e in
                           let [|- refl] = ev_det [|- D] [|- D'] in
                           ExSimlam [|- ev-val val-lam] s'
;
rec ev_sim : [|- eval E2 E2'] -> Sim [|- T] [|- E1] [|- E2'] -> Sim [|- T] [|- E1] [|- E2] =
fun [|- D] s .Sim_lam e => let ExSimlam [|- D'] s' = Sim_lam s e in
                           let [|- refl] = ev_val [|- D] [|- D'] in
                           ExSimlam [|- D] s'
  | [|- D] s .Sim_top e => let [|- D'] = Sim_top s e in
                           let [|- refl] = ev_val [|- D] [|- D'] in
                           [|- D]
  | [|- D] s .Sim_nil e => let [|- D'] = Sim_nil s e in
                           let [|- refl] = ev_val [|- D] [|- D'] in
                           [|- D]
  | [|- D] s .Sim_cons e => let ExSimcons [|- D'] s1 s2 = Sim_cons s e in
                           let [|- refl] = ev_val [|- D] [|- D'] in
                           ExSimcons [|- D] s1 s2
;

rec fix_sim : {M:[x:exp T |- exp T[]]} Sim [ |- T] [|- M [fix \x.M]] [|- fix \x.M] =
fun [x:exp top |- M] .Sim_top [|- D] => [|- ev-fix (\x.M) D]
  | [x:exp (list T) |- M] .Sim_nil [|- D] => [|- ev-fix (\x.M) D]
  | [x:exp (list T) |- M] .Sim_cons [|- D] =>
    ExSimcons [|- ev-fix (\x.M) D] (sim_refl [|- _]) (sim_refl [|- _])
  | [x:exp (arr S T) |- M] .Sim_lam (d : [|- eval (M[fix \x.M]) (lam \x.E)]) =>
    let [|- D] = d in
    ExSimlam [|- ev-fix (\x.M) D] (mlam R => sim_refl [|- E[R]])
;

rec case_nil_sim : [|- eval L nil] -> Sim [ |- T] [|- M] [|- lcase L M \x.\y.N] =
fun [|- D0] .Sim_top [|- D] => [|- ev-case-nil D0 D]
  | [|- D0] .Sim_nil [|- D] => [|- ev-case-nil D0 D]
  | [|- D0] .Sim_cons [|- D] =>
    ExSimcons [|- ev-case-nil D0 D] (sim_refl [|- _]) (sim_refl [|- _])
  | [|- D0] .Sim_lam
              (d : [ |- eval M (lam \x. E1')]) => let [|- D] = d in
ExSimlam [|- ev-case-nil D0 D] (mlam R => sim_refl [|- E1'[R]])
;

rec case_cons_sim : [|- eval L (cons LH LT)] -> {N:[x:exp S, y:exp (list S[]) |- exp T[]]}
                    Sim [ |- T] [|- N[LH,LT]] [|- lcase L M \x.\y.N] =
fun [|- D0] [x:exp _, y:exp _ |- N] .Sim_top [|- D] => [|- ev-case-cons (\x.\y.N) D0 D]
  | [|- D0] [x:exp _, y:exp _ |- N] .Sim_nil [|- D] => [|- ev-case-cons (\x.\y.N) D0 D]
  | [|- D0] [x:exp _, y:exp _ |- N] .Sim_cons [|- D] =>
    ExSimcons [|- ev-case-cons (\x.\y.N) D0 D] (sim_refl [|- _]) (sim_refl [|- _])
  | [|- D0] [x:exp _, y:exp _ |- N] .Sim_lam (d : [|- eval (N[LH,LT]) (lam \x.E)]) =>
    let [|- D] = d in
    ExSimlam [|- ev-case-cons (\x.\y.N) D0 D] (mlam R => sim_refl [|- E[R]])
;

rec down_closed : [|- eval E1 V] -> Howe [|- T] [|- E1] [|- E2] -> Howe [|- T] [|- V] [|- E2] =
fun [ |- ev-val V] h => h
  | [ |- ev-app D1 D2] (Howe_app h1 (h2:Howe [ |- _] [ |- E2] [ |- E2']) (OSimC [ _ ] s)) =>
    let Howe_lam h' (OSimC [ _ ] s') = down_closed [|- D1] h1 in
    let ExSimlam ([|- D1']:[|- eval _ (lam \x.M)]) s'' =
        Sim_lam (s' [|- ^]) [|- ev-val val-lam] in
    let ha = howe_osim_trans (howe_subst h' (HCons HNil h2)) (sim_osim (s'' [|- E2'])) in
    let ha1 = down_closed [|- D2] ha in
    let Howe_lam ha' (OSimC [ _ ] sa) = ha1 in
    let ExSimlam ([|- D2']:[|- eval N0 (lam \x.N)]) sa' =
        Sim_lam (sa [|- ^]) [|- ev-val val-lam] in
    let ExSimlam (dd:[|- eval _ (lam \x.N')]) ss =
        Sim_lam (s [|- ^]) [|- ev-app D1' D2'] in
    let s1 = ev_sim' [|- D2'] (sim_refl [|- N0]) in
    let s2 = ev_sim dd (sim_lam [x:exp _ |- N] [x:exp _ |- N'] ss) in
    howe_osim_trans ha1 (sim_osim (sim_trans s1 s2))
  | [ |- ev-fix (\x._) D] (Howe_fix (h1:Howe [ |- T] [x:exp T |- M] [x:exp T |- M']) s1) =>
    let h' = Howe_fix h1 (osim_refl [|- fix \x.M']) in
    let h'' = howe_subst h1 (HCons HNil h') in
    howe_osim_trans (down_closed [|- D] h'')
                    (osim_trans (sim_osim (fix_sim [x:exp _ |- _])) s1)
  | [ |- ev-case-nil D1 D2] (Howe_case h1 h2 h3 s) =>
    let s' = case_nil_sim (howe_ev_nil (down_closed [|- D1] h1)) in
    howe_osim_trans (down_closed [|- D2] h2) (osim_trans (sim_osim s') s)
  | [ |- ev-case-cons (\x.\y._) D1 D2] (Howe_case h1 h2 h3 s) =>
    let HoweConsC [ |- D1'] (h1':Howe [ |- T] [ |- M'] [ |- N'])
                  (h2':Howe [ |- list T] [ |- MS'] [ |- NS'])
        = howe_ev_cons (down_closed [|- D1] h1) in
    let s' = case_cons_sim [ |- D1'] [x:exp _,y:exp (list _) |- _] in
    let h_hat = down_closed [|- D2] (howe_subst h3 (HCons (HCons HNil h1') h2')) in
    howe_osim_trans h_hat (osim_trans (sim_osim s') s)
;

rec howe_sim : Howe [|- T] [|- E1] [|- E2] -> Sim [|- T] [|- E1] [|- E2] =
fun h .Sim_lam e => let HoweAbsC e' f = howe_ev_abs (down_closed e h) in
                        ExSimlam e' (mlam E => howe_sim (f [ |- E]))
  | h .Sim_top e => howe_ev_unit (down_closed e h)
  | h .Sim_nil e => howe_ev_nil (down_closed e h)
  | h .Sim_cons e => let HoweConsC e' h1 h2 = howe_ev_cons (down_closed e h) in
                         ExSimcons e' (howe_sim h1) (howe_sim h2)
;

rec howe_osim : (g:ctx) Howe [|- T] [g |- E1] [g |- E2] -> OSim [|- T] [g |- E1] [g |- E2] =
    fun h =>
    let h: Howe [|- T] [g |- E] [g |- E'] = h in
    OSimC [g] (mlam #S => howe_sim (howe_subst h (howeSubst_refl [g] [] [|- #S])))
;

%%%%%%%%%

let succ : [ |- exp (arr (arr (arr B D) (arr A B)) (arr (arr B D) (arr A D))) ]
    = [|- lam \n. lam \x. lam \y. app x (app (app n x) y)];
% succ

let one : [ |- exp (arr (arr S S) (arr S S)) ] = [|- lam \f.lam \x.app f x];
% one

let so : [|- exp (arr (arr S S) (arr S S))] = [|- app (lam \n. lam \x. lam \y. app x (app (app n x) y)) (lam \f.lam \x.app f x)];
% succ one

let two : [ |- exp (arr (arr S S) (arr S S))] = [|- lam \f.lam \x.app f (app f x)];
% two


inductive Kless : [|- exp T] -> [|- exp T] -> ctype =
  | Kle : ({V:[|- exp T]} [|- eval E V] -> [|- eval F V]) -> Kless [|- E] [|- F]
;

rec beta_d : {V :[|- exp _]} {W :[|- exp _]} {M :[|- exp T]} [|- eval (app V W) M]
             -> [|- eval (app (app (lam \x. (lam \y. app x y)) V) W) M] =
    fun [|- V] [|- W] [|- M] [|- D] => [|- ev-app (ev-app (ev-val val-lam) (ev-val val-lam)) D]
;

rec kle_beta_d : {V :[|- exp _]} {W :[|- exp _]}
                 Kless [|- app V W] [|- app (app (lam \f. (lam \y. app f y)) V) W] =
    fun [|- V] [|- W] => Kle (beta_d [|- V] [|- W])
;

rec kleene_sim : Kless [|- M] [ |- N] -> Sim [|- T] [|- M] [|- N] =
fun (Kle f) .Sim_lam ([|- D] : [ |- eval M (lam (\x. M'))]) =>
  ExSimlam (f [|- _] [|- D]) (mlam E => sim_refl [|- M' [E]])
| (Kle f) .Sim_top [|- D] => f [|- _] [|- D]
| (Kle f) .Sim_nil [|- D] => f [|- _] [|- D]
| (Kle f) .Sim_cons [|- D] => ExSimcons (f [|- _] [|- D]) (sim_refl [|- _]) (sim_refl [|- _])
;

rec howe_cong_app : Howe [|- arr S T] [|- M1] [|- M2] -> Howe [|- S] [|- N1] [|- N2]
                    -> Howe [|- T] [|- app M1 N1] [|- app M2 N2] =
    fun h1 h2 => Howe_app h1 h2 (osim_refl [|- _])
;

rec sim_cong_app : Sim [|- arr S T] [|- M1] [|- M2] -> Sim [|- S] [|- N1] [|- N2] -> Sim [|- T] [|- app M1 N1] [|- app M2 N2] =
    fun s1 s2 => howe_sim (howe_cong_app (sim_howe s1) (sim_howe s2))
;

rec ev1 : [|- eval (app E F1) V] -> [|- eval (app (app (lam (\f. lam (\w. app f w))) E) F1) V] =
fun [|- D] => [|- ev-app (ev-app (ev-val val-lam) (ev-val val-lam)) D]
;

rec test : Sim [ |- T] [ |- app E F1] [ |- app (app (lam (\f. lam (\w. app f w))) E) F1] =
    fun .Sim_lam (d : [ |- eval (app E F1) (lam (\x. E1'))])  =>
       ExSimlam (ev1 d) (mlam V => sim_refl [|- E1'[V]])
| .Sim_top [|- D] => ev1 [|- D]
| .Sim_nil [|- D] => ev1 [|- D]
| .Sim_cons [|- D] => ExSimcons (ev1 [|- D]) (sim_refl [|- _]) (sim_refl [|- _])
;

rec sim_lemma1:{E:[|- exp (arr T T)]}
              Sim [|- arr T T]
                  [|-  lam (\x. app E[] (app E[] x))]
                  [ |- lam (\y. app E[] (app (app (lam (\f. lam (\w. app f w))) E[]) y))] =
fun [|- E] .Sim_lam [|- ev-val val-lam] =>
     ExSimlam [|- ev-val val-lam]
              (mlam F1 => sim_cong_app (sim_refl [|- E]) test)
                          % (kleene_sim (kle_beta_d [|- E] [|- F1])))
;

rec evlam : [ |- eval (app (lam (\n. lam (\x. lam (\y. app x (app (app n x) y))))) (lam (\f. lam (\x. app f x))))
                                    (lam \x. lam (\y. app x (app (app (lam (\f. lam (\w. app f w))) x) y)))] =
[|- ev-app (ev-val val-lam) (ev-val val-lam)] ;

rec sim_two_succ_one : Sim [|- arr (arr T0 T0) (arr T0 T0)]
                           [|- lam \f.lam \x.app f (app f x)]
                           [|- app (lam \n. lam \x. lam \y. app x (app (app n x) y)) (lam \f.lam \x.app f x)] =
   fun .Sim_lam [|- ev-val val-lam] => ExSimlam evlam sim_lemma1
;

rec sim_lem2 : {E : [ |- exp (arr T0 T0)]}
                   Sim [ |- arr T0 T0] [ |- lam (\x. app E[] (app E[] x))]
                                       [ |- lam (\y. app E[] (app (app (lam (\f. lam (\w. app f w))) E[]) y))] =
fun [|- E] .Sim_lam [|- ev-val val-lam] =>
ExSimlam [|- ev-val val-lam] (mlam E1 => ?)
;

rec sim_two_succ_one' : Sim [|- arr (arr T0 T0) (arr T0 T0)]
                           [|- lam \f.lam \x.app f (app f x)]
                           [|- app (lam \n. lam \x. lam \y. app x (app (app n x) y)) (lam \f.lam \x.app f x)] =
   fun .Sim_lam [|- ev-val val-lam] => ExSimlam evlam sim_lem2
;
