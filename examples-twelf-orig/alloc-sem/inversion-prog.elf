%%% Simply Typed Lambda Calculus w/ small-step, allocation semantics
%%% Author: Matthew Fluet (June 2005)
%%% inversion-prog.elf

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% Store Typing Inversion (Progress)
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
prog_st_judg_inversion_aux
               : sttp_wf ST -> st_wf S -> st_judg S ST ->
                 sttp_lookup ST L T
                  -> st_lookup S L V ->
                     val_judg ST V T -> type.
%mode prog_st_judg_inversion_aux +DSTWf +DSWf +DStTcWf +SttpLookup -StLookup -DValTc.
-hit           : prog_st_judg_inversion_aux DSTWf DSWf (st_judg_cons DValTc' _)
                                         (sttp_lookup_hit)
                                         (st_lookup_hit)
		                         DValTc
		  <- sttp_wf_cons_implies_extend_cons DSTWf DSttpExtend
		  <- sttp_weak_val_judg DValTc' DSttpExtend DValTc.
-miss          : prog_st_judg_inversion_aux DSTWf DSWf (st_judg_cons _ DStTc')
                                         (sttp_lookup_miss DSttpLookup' DLocLt)
                                         (st_lookup_miss DStLookup' DLocLt)
                                         DValTc
		  <- sttp_wf_tail DSTWf DSTWf'
		  <- st_wf_tail DSWf DSWf'
		  <- prog_st_judg_inversion_aux DSTWf' DSWf' DStTc'
                                             DSttpLookup'
                                             DStLookup'
                                             DValTc'
		  <- sttp_wf_cons_implies_extend_cons DSTWf DSttpExtend'
		  <- sttp_weak_val_judg DValTc' DSttpExtend' DValTc.
%terminates DSttpLookup (prog_st_judg_inversion_aux _ _ _ DSttpLookup _ _).
%worlds () (prog_st_judg_inversion_aux _ _ _ _ _ _).
%covers prog_st_judg_inversion_aux +DSTWf +DSWf +DStTc -SttpLookup +StLookup -DValTc.
%total DSttpLookup (prog_st_judg_inversion_aux _ _ _ DSttpLookup _ _).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

prog_st_judg_inversion
               : stwf_judg S ST ->
                 sttp_lookup ST L T
                  -> st_lookup S L V ->
                     val_judg ST V T -> type.
%mode prog_st_judg_inversion +DStTcWf +SttpLookup -StLookup -DValTc.
-              : prog_st_judg_inversion (stwf_judg_ DSTWf DSWf DStTc)
                                     DSttpLookup
                                     DStLookup
                                     DValTc
		  <- prog_st_judg_inversion_aux DSTWf DSWf DStTc
                                             DSttpLookup
                                             DStLookup
                                             DValTc.
%terminates {} (prog_st_judg_inversion _ _ _ _).
%worlds () (prog_st_judg_inversion _ _ _ _).
%covers prog_st_judg_inversion +DStTcWf +SttpLookup -StLookup -DValTc.
%total {} (prog_st_judg_inversion _ _ _ _).
