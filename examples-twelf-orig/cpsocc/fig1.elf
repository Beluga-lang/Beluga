%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%% Figure 1 : The left-to-right, call-by-value CPS transformation
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


cpsR : droot -> croot -> type.                     %name cpsR CR.
cpsE : dexp -> (ctriv -> cexp) -> cexp -> type.    %name cpsE CE.
cpsT : dtriv -> ctriv -> type.                     %name cpsT CT.
%mode cpsR +R -R'.
%mode cpsE +E +K -E'.
%mode cpsT +T -T'.

cps_droot : cpsR (dexp->droot E) (klam ([x] E' x))
            <- {k:ccont} cpsE E ([v] cret k v) (E' k).

cps_dtriv : cpsE (dtriv->dexp T) ([x] K  x) (K T')
            <- cpsT T T'.

cps_dapp : cpsE (dapp E0 E1) ([x] K  x) E'
           <- ({t0:ctriv}
               cpsE E1 ([t1] capp t0 t1 (vlam ([x] K x))) (E1' t0))
           <- cpsE E0 ([x] E1' x) E'.

cps_dlam : cpsT (dlam ([x] R x)) (xlam ([x] R' x))
           <- ({x:dtriv}{x':ctriv}cpsT x x' -> cpsR (R x) (R' x')).

%terminates (R E T) (cpsR R R') (cpsE E K E')  (cpsT T T').
