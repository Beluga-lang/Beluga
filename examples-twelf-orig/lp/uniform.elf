%%% Uniform Derivations
%%% Author: Frank Pfenning

solve  : o -> type.       % solve goal formulas
assume : o -> type.       % assume program formulas
entail : o -> p -> type.  % immediate entailment
%infix none 8 >>.
%name solve S.
%name assume H.
%name entail I.
%mode solve *A.
%mode assume *A.
%mode >> *A *P.

s_and  : solve (& A1 A2)
	  <- solve A1
	  <- solve A2.

s_imp  : solve (imp A2 A1)
	  <- (assume A2 -> solve A1).

s_true : solve (true).

s_forall : solve (forall A1)
	    <- {a:i} solve (A1 a).

s_atom : solve (atom P)
	  <- assume A
	  <- entail A P.

i_andl : entail (& A1  A2) P
	   <- entail A1 P.

i_andr : entail (& A1 A2) P
	   <- entail A2 P.

i_imp  : entail (imp A2 A1)  P
	  <- entail A1 P
	  <- solve A2.

i_forall : {T:i}
	     entail (forall A1) P
	     <- entail (A1 T)  P.

i_atom : entail (atom P) P.
