%%% The language and inference rules of natural deduction
%%% This contains only the positive fragment
%%% Author: Frank Pfenning

i : type.  % individuals
o : type.  % formulas
p : type.  % atomic formulas
%name i T. % S
%name o A. % B C
%name p P. % Q

atom   : p -> o.
conj      : o -> o -> o.  %% infix right 11 conj.
imp    : o -> o -> o.  %% infix right 10 imp.
true   : o.
forall : (i -> o) -> o.

pf : o -> type.  % natural deductions
%name pf D. % E

andi    : pf B -> pf A -> pf (conj A B).
andel   : pf (conj A B) -> pf A.
ander   : pf (conj A B) -> pf B.

impi    : (pf A -> pf B) -> pf (imp A B).
impe    : pf (imp A B) -> pf A -> pf B.

truei   : pf (true).

foralli : ({a:i} pf (A a)) -> pf (forall (\x. A x)).
foralle : pf (forall (\x. A x)) -> {T:i} pf (A T).
