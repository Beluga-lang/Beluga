% Closure based interpreter with computation-level data-types.
% Author: Brigitte Pientka

LF tm : type =
| z    : tm
| suc  : (tm) -> tm
| app  : tm -> tm -> tm
| lam  : (tm -> tm) -> tm
;

schema tctx = tm ;

LF nat: type =
| zero   : nat
| succ : nat -> nat
;

inductive NList : [ |- nat] -> ctype   =
| NNil : NList [ |- zero]
| NCons : {g:tctx} {N:[ |- nat]} [g |- tm] -> NList [ |- N] -> NList [ |- succ N]
;

inductive List : ctype  =
| Nil : List
| Cons : {g:tctx} [g |- tm] -> List -> List
;

inductive Clos : ctype =
Cl : (g:tctx)  [g,x:tm  |-  tm] -> ([g |- tm] -> Clos) -> Clos
;

inductive CtxRel : {g:tctx} {h:tctx} ctype  =
| Rnil : CtxRel [ ] [ ]
| Rsnoc : (g:tctx) (h:tctx) CtxRel [g] [h] -> CtxRel [g, x:tm] [h, x:tm]
;

typedef Env : {g:tctx} ctype = [g |- tm] -> Clos;


rec test : Clos -> Clos =
/ total x (test x)/
fn x => case x of Cl y f  => x
;

rec eval : (g:tctx) [g |- tm] -> ([g |- tm] -> Clos) -> Clos =
fn e => fn env => case e of
| [g |- #p ] => env [g |- #p]
| [g |- lam \x. E] => Cl [g, x:tm  |-  E] env
| [g |- app E1 E2] =>
  case eval [g |- E1] env of
  | Cl [h, x:tm  |-  E] env' =>
    let v = eval [g |- E2] env in
    eval [h,x:tm  |-  E]
         (fn var => case var of
               | [h, x:tm  |-  x] => v
               | [h, x:tm  |-  #p[..] ] => env' [h |- #p]
         )

;


% Using type abbreviations
rec eval : (g:tctx) [g |- tm] -> Env [g] -> Clos =
fn e => fn env => case e of
| [g |- #p ] => env [g |- #p]
| [g |- lam \x. E] => Cl [g, x:tm  |-  E] env
| [g |- app E1 E2] =>
  case eval [g |- E1] env of
  | Cl [h, x:tm  |-  E] env' =>
    let v = eval [g |- E2] env in
    eval [h,x:tm  |-  E]
         (fn var => case var of
               | [h, x:tm  |-  x] => v
               | [h, x:tm  |-  #p[..] ] => env' [h |- #p]
         )
;
